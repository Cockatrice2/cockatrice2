!include ..\..\..\NSIS.definitions.nsh
!include "MUI2.nsh"
!include "FileFunc.nsh"

Name "@CPACK_PACKAGE_NAME@"
BrandingText "@CPACK_PACKAGE_NAME@-@CPACK_PACKAGE_VERSION_MAJOR@"
OutFile "@CPACK_TOPLEVEL_DIRECTORY@/@CPACK_OUTPUT_FILE_NAME@"
SetCompressor /SOLID lzma
InstallDir "$PROGRAMFILES\Cockatrice2"
!define INST_DIR "@CPACK_TEMPORARY_DIRECTORY@"

!define MUI_ABORTWARNING
!define MUI_WELCOMEFINISHPAGE_BITMAP "${NSIS_SOURCE_PATH}\cmake\leftimage.bmp"
!define MUI_UNWELCOMEFINISHPAGE_BITMAP "${NSIS_SOURCE_PATH}\cmake\leftimage.bmp"
!define MUI_HEADERIMAGE
!define MUI_HEADERIMAGE_BITMAP "${NSIS_SOURCE_PATH}\cmake\headerimage.bmp"
!define MUI_HEADERIMAGE_UNBITMAP "${NSIS_SOURCE_PATH}\cmake\headerimage.bmp"
!define MUI_WELCOMEPAGE_TEXT "This wizard will guide you through the installation of Cockatrice2.$\r$\n$\r$\nClick Next to continue."
!define MUI_FINISHPAGE_RUN "$INSTDIR/cockatrice2.exe"
!define MUI_FINISHPAGE_RUN_TEXT "Run 'Cockatrice2' now"

!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_LICENSE "${NSIS_SOURCE_PATH}\COPYING"
!insertmacro MUI_PAGE_COMPONENTS
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES
!insertmacro MUI_PAGE_FINISH

!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_COMPONENTS
!insertmacro MUI_UNPAGE_INSTFILES
!insertmacro MUI_UNPAGE_FINISH

!insertmacro MUI_LANGUAGE "English"

Section "Application" SecApplication
	SetShellVarContext all
	SetOutPath "$INSTDIR"

    @CPACK_NSIS_EXTRA_PREINSTALL_COMMANDS@
    @CPACK_NSIS_FULL_INSTALL@

	WriteUninstaller "$INSTDIR\uninstall.exe"
	${GetSize} "$INSTDIR" "/S=0K" $0 $1 $2
	IntFmt $0 "0x%08X" $0
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Cockatrice2" "DisplayName" "Cockatrice2"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Cockatrice2" "UninstallString" "$\"$INSTDIR\uninstall.exe$\""
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Cockatrice2" "QuietUninstallString" "$\"$INSTDIR\uninstall.exe$\" /S"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Cockatrice2" "InstallLocation" "$INSTDIR"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Cockatrice2" "DisplayIcon" "$INSTDIR\cockatrice2.exe"
	WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Cockatrice2" "EstimatedSize" "$0"
SectionEnd

Section "Update configuration" SecUpdateConfig
	SetShellVarContext current
	WriteRegStr HKCU "Software\Cockatrice2\Cockatrice2\paths" "carddatabase" "$LOCALAPPDATA\Cockatrice2\cards.xml"
	WriteRegStr HKCU "Software\Cockatrice2\Cockatrice2\paths" "tokendatabase" "$LOCALAPPDATA\Cockatrice2\tokens.xml"
	WriteRegStr HKCU "Software\Cockatrice2\Cockatrice2\paths" "decks" "$LOCALAPPDATA\Cockatrice2\decks"
	WriteRegStr HKCU "Software\Cockatrice2\Cockatrice2\paths" "pics" "$LOCALAPPDATA\Cockatrice2\pics"
	WriteRegStr HKCU "Software\Cockatrice2\Cockatrice2\replays" "pics" "$LOCALAPPDATA\Cockatrice2\replays"
	WriteRegStr HKCU "Software\Cockatrice2\Cockatrice2\sound" "path" "$INSTDIR\sounds"
SectionEnd

Section "Start menu item" SecStartMenu
	SetShellVarContext all
	createDirectory "$SMPROGRAMS\Cockatrice2"
	createShortCut "$SMPROGRAMS\Cockatrice2\Cockatrice2.lnk" "$INSTDIR\cockatrice2.exe"
SectionEnd

Section "un.Application" UnSecApplication
	SetShellVarContext all
	RMDir /r "$INSTDIR\plugins"
	RMDir /r "$INSTDIR\sounds"
	RMDir /r "$INSTDIR\themes"
	RMDir /r "$INSTDIR\translations"
	Delete "$INSTDIR\uninstall.exe"
	Delete "$INSTDIR\cockatrice2.exe"
	Delete "$INSTDIR\Qt*.dll"
	Delete "$INSTDIR\libmysql.dll"
	Delete "$INSTDIR\icu*.dll"
	Delete "$INSTDIR\libeay32.dll"
	Delete "$INSTDIR\ssleay32.dll"
	Delete "$INSTDIR\qt.conf"
	Delete "$INSTDIR\qdebug.txt"
	Delete "$INSTDIR\zlib*.dll"
	RMDir "$INSTDIR"

	RMDir "$SMPROGRAMS\Cockatrice2"

	DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Cockatrice2"
SectionEnd

; unselected because it is /o
Section /o "un.Configurations, decks, cards, pics" UnSecConfiguration
	SetShellVarContext current
	DeleteRegKey HKCU "Software\Cockatrice2"

	RMDir /r "$LOCALAPPDATA\Cockatrice2"
SectionEnd

LangString DESC_SecApplication ${LANG_ENGLISH} "Cockatrice2 program files"
LangString DESC_SecUpdateConfig ${LANG_ENGLISH} "Update the paths in the application settings according to the installation paths."
LangString DESC_SecStartMenu ${LANG_ENGLISH} "Create start menu items for Cockatrice2"
!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
	!insertmacro MUI_DESCRIPTION_TEXT ${SecApplication} $(DESC_SecApplication)
	!insertmacro MUI_DESCRIPTION_TEXT ${SecUpdateConfig} $(DESC_SecUpdateConfig)
	!insertmacro MUI_DESCRIPTION_TEXT ${SecStartMenu} $(DESC_SecStartMenu)
!insertmacro MUI_FUNCTION_DESCRIPTION_END

LangString DESC_UnSecApplication ${LANG_ENGLISH} "Cockatrice2 program files and start menu items"
LangString DESC_UnSecConfiguration ${LANG_ENGLISH} "Configurations, decks, card database, pictures"
!insertmacro MUI_UNFUNCTION_DESCRIPTION_BEGIN
	!insertmacro MUI_DESCRIPTION_TEXT ${UnSecApplication} $(DESC_UnSecApplication)
	!insertmacro MUI_DESCRIPTION_TEXT ${UnSecConfiguration} $(DESC_UnSecConfiguration)
!insertmacro MUI_UNFUNCTION_DESCRIPTION_END


Function .onInit
    SetShellVarContext all
    ReadRegStr $R0 HKLM "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\Cockatrice2" "UninstallString"
    StrCmp $R0 "" done

    MessageBox MB_OKCANCEL|MB_ICONEXCLAMATION "A previous version of Cockatrice2 must be uninstalled before installing the new one." IDOK uninst
    Abort

uninst:
    ClearErrors
    ExecWait "$R0"

done:

FunctionEnd